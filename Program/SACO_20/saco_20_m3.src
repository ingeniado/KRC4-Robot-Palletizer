&ACCESS RVO1
&REL 32
&PARAM DISKPATH = KRC:\R1\Program\SACO_20
&PARAM TEMPLATE = C:\KRC\Roboter\Template\vorgabe
&PARAM EDITMASK = *
DEF saco_20_m3( )
;Fold DECLARACION VARIABLES
DECL E6POS POS_A
DECL E6POS POS_B
DECL E6POS POS_C
DECL REAL INC_Z
DECL INT PLANTA
DECL INT SACOS_A_PALETIZAR
DECL E6POS PUNTO[10]
DECL INT n
EXT COGE_20 () 
;Endfold
;FOLD INI;%{PE}
  ;FOLD BASISTECH INI
    GLOBAL INTERRUPT DECL 3 WHEN $STOPMESS==TRUE DO IR_STOPM ( )
    INTERRUPT ON 3 
    BAS (#INITMOV,0 )
  ;ENDFOLD (BASISTECH INI)
;FOLD SPOTTECH INI
USERSPOT(#INIT)
;ENDFOLD (SPOTTECH INI)
;FOLD GRIPPERTECH INI
USER_GRP(0,DUMMY,DUMMY,GDEFAULT)
;ENDFOLD (GRIPPERTECH INI)
  ;FOLD USER INI
    ;Make your modifications here

  ;ENDFOLD (USER INI)
;ENDFOLD (INI)
; === PROGRAMA 36 SACOS 20 KG PISON  BOX==
;Fold VARIABLES
INC_Z=0  
PLANTA = 1
N_SACO=0
SACOS_A_PALETIZAR=36
n=1
PUNTO[1]=XP1
PUNTO[2]=XP2
PUNTO[3]=XP3
PUNTO[4]=XP4
PUNTO[5]=XP5
PUNTO[6]=XP6
PUNTO[7]=XP7
PUNTO[8]=XP8
PUNTO[9]=XP9
PUNTO[10]=XP10
;Endfold
;FOLD PTP HOME  Vel= 100 % DEFAULT;%{PE}%MKUKATPBASIS,%CMOVE,%VPTP,%P 1:PTP, 2:HOME, 3:, 5:100, 7:DEFAULT
$BWDSTART = FALSE
PDAT_ACT=PDEFAULT
FDAT_ACT=FHOME
BAS (#PTP_PARAMS,100 )
$H_POS=XHOME
PTP  XHOME
;ENDFOLD
;Fold Compruebo Palet
WAIT FOR PALET_LIBRE OR SALIR_PROGRAMA_ACTUAL
IF SALIR_PROGRAMA_ACTUAL == TRUE THEN
   PULSE ( OK_LEIDO_SALIR_PROGRAMA, true, 0.5 )
   WAIT SEC 1
   GOTO SALIRPROG
ENDIF
;Endfold
BOMBA =TRUE
;=== BUCLE PRINCIPAL ==
FOR PLANTA=1 TO 12 ;Bucle de plantas
FOR n=1 TO 10 ;Bucle de sacos
SACO:
;Fold DEFINICION PUNTOS AUXILIARES

SWITCH n

CASE 1
;Aproximacion Superior Derecha
POS_A =PUNTO[n]
POS_B =PUNTO[n]
POS_C =PUNTO[n]

POS_A.Z=POS_A.Z+(INC_Z/2)+1100
POS_A.X=POS_A.X-80
POS_A.Y=POS_A.Y+80

POS_B.Z=POS_B.Z+INC_Z+130
POS_B.X=POS_B.X-80
POS_B.Y=POS_B.Y+80

POS_C.Z=POS_C.Z+INC_Z+30

CASE 2
;Aproximacion Superior Izquierda
POS_A =PUNTO[n]
POS_B =PUNTO[n]
POS_C =PUNTO[n]

POS_A.Z=POS_A.Z+(INC_Z/2)+1100
POS_A.X=POS_A.X-80
POS_A.Y=POS_A.Y-80

POS_B.Z=POS_B.Z+INC_Z+130
POS_B.X=POS_B.X-80
POS_B.Y=POS_B.Y-80

POS_C.Z=POS_C.Z+INC_Z+30

CASE 3
;Aproximacion Inferior Izquierda
POS_A =PUNTO[n]
POS_B =PUNTO[n]
POS_C =PUNTO[n]

POS_A.Z=POS_A.Z+(INC_Z/2)+1100
POS_A.X=POS_A.X+80
POS_A.Y=POS_A.Y-80

POS_B.Z=POS_B.Z+INC_Z+130
POS_B.X=POS_B.X+80
POS_B.Y=POS_B.Y-80

POS_C.Z=POS_C.Z+INC_Z+30

CASE 4
;Aproximacion Inferior Derecha
POS_A =PUNTO[n]
POS_B =PUNTO[n]
POS_C =PUNTO[n]

POS_A.Z=POS_A.Z+(INC_Z/2)+1100
POS_A.X=POS_A.X+80
POS_A.Y=POS_A.Y+80

POS_B.Z=POS_B.Z+INC_Z+130
POS_B.X=POS_B.X+80
POS_B.Y=POS_B.Y+80

POS_C.Z=POS_C.Z+INC_Z+30

CASE 5
;Aproximacion Inferior Centro
POS_A =PUNTO[n]
POS_B =PUNTO[n]
POS_C =PUNTO[n]

POS_A.Z=POS_A.Z+(INC_Z/2)+1100
POS_A.X=POS_A.X+80

POS_B.Z=POS_B.Z+INC_Z+130
POS_B.X=POS_B.X+80

POS_C.Z=POS_C.Z+INC_Z+30

CASE 6
;Aproximacion Inferior Izquierda
POS_A =PUNTO[n]
POS_B =PUNTO[n]
POS_C =PUNTO[n]

POS_A.Z=POS_A.Z+(INC_Z/2)+1100
POS_A.X=POS_A.X+80
POS_A.Y=POS_A.Y-80

POS_B.Z=POS_B.Z+INC_Z+130
POS_B.X=POS_B.X+80
POS_B.Y=POS_B.Y-80

POS_C.Z=POS_C.Z+INC_Z+30

CASE 7
;Aproximacion Inferior Derecha
POS_A =PUNTO[n]
POS_B =PUNTO[n]
POS_C =PUNTO[n]

POS_A.Z=POS_A.Z+(INC_Z/2)+1100
POS_A.X=POS_A.X+80
POS_A.Y=POS_A.Y+80

POS_B.Z=POS_B.Z+INC_Z+130
POS_B.X=POS_B.X+80
POS_B.Y=POS_B.Y+80

POS_C.Z=POS_C.Z+INC_Z+30

CASE 8
;Aproximacion Superior Derecha
POS_A =PUNTO[n]
POS_B =PUNTO[n]
POS_C =PUNTO[n]

POS_A.Z=POS_A.Z+(INC_Z/2)+1100
POS_A.X=POS_A.X-80
POS_A.Y=POS_A.Y+80

POS_B.Z=POS_B.Z+INC_Z+130
POS_B.X=POS_B.X-80
POS_B.Y=POS_B.Y+80

POS_C.Z=POS_C.Z+INC_Z+30

CASE 9
;Aproximacion Superior Izquierda
POS_A =PUNTO[n]
POS_B =PUNTO[n]
POS_C =PUNTO[n]

POS_A.Z=POS_A.Z+(INC_Z/2)+1100
POS_A.X=POS_A.X-80
POS_A.Y=POS_A.Y-80

POS_B.Z=POS_B.Z+INC_Z+130
POS_B.X=POS_B.X-80
POS_B.Y=POS_B.Y-80

POS_C.Z=POS_C.Z+INC_Z+30

CASE 10
;Aproximacion Superior Centro
POS_A =PUNTO[n]
POS_B =PUNTO[n]
POS_C =PUNTO[n]

POS_A.Z=POS_A.Z+(INC_Z/2)+1100
POS_A.X=POS_A.X-80

POS_B.Z=POS_B.Z+INC_Z+130
POS_B.X=POS_B.X-80

POS_C.Z=POS_C.Z+INC_Z+30

DEFAULT
ENDSWITCH

IF  (N_SACO >= 35) THEN
POS_A =PUNTO[4]
POS_B =PUNTO[4]
POS_C =PUNTO[4]


POS_A.Z=POS_A.Z+(INC_Z/2)+1100+230
POS_A.X=POS_A.X-80
POS_A.Y=POS_A.Y-80

POS_B.Z=POS_B.Z+INC_Z+130+230
POS_B.X=POS_B.X-80
POS_B.Y=POS_B.Y-80

POS_C.Z=POS_C.Z+INC_Z+30+200
ENDIF

;Endfold

;Fold WAIT FOR REPITE_SACO: Espero repetir saco si ha caido
IF (ERROR_SACO_CAIDO == TRUE ) THEN
   WAIT FOR REPITE_SACO OR SALIR_PROGRAMA_ACTUAL
   ERROR_SACO_CAIDO = FALSE
ENDIF
;Endfold

COGE_20 () ;Llamo programa coger
;Fold CONDICION DE SALIDA DE PROGRAMA PLC
IF SALIR == TRUE THEN
   GOTO SALIRPROG
ENDIF
;Endfold

BAS(#BASE,2) ;Cambio de base
$APO.CPTP = 70

;Fold Velocidad SACO 20 Kg
FOR ax_num=1 TO 6
$VEL_AXIS[ax_num]=vptpSACO20
$ACC_AXIS[ax_num]=vaccSACO20
ENDFOR
$VEL.CP=vlinSACO20
;Endfold

PTP POS_A C_PTP

LIN POS_B C_DIS

;Fold Velocidad Lenta
FOR ax_num=1 TO 6
$VEL_AXIS[ax_num]=vptpL
$ACC_AXIS[ax_num]=vaccL
ENDFOR
$VEL.CP=vlinL
;Endfold

LIN POS_C

;Fold Compruebo caida de saco
IF (VACOESTATO == FALSE) THEN ;Condicion de saco perdido
   ERROR_SACO_CAIDO = TRUE
   WAIT SEC 0.2
   VACIO=FALSE  ;Abro vacio
   
   FOR ax_num=1 TO 6
   $VEL_AXIS[ax_num]=vptpR
   $ACC_AXIS[ax_num]=vaccR
   ENDFOR
   $VEL.CP=vlinR
   LIN POS_A
   ;FOLD PTP HOME  Vel= 100 % DEFAULT;%{PE}%MKUKATPBASIS,%CMOVE,%VPTP,%P 1:PTP, 2:HOME, 3:, 5:100, 7:DEFAULT
$BWDSTART = FALSE
PDAT_ACT=PDEFAULT
FDAT_ACT=FHOME
BAS (#PTP_PARAMS,100 )
$H_POS=XHOME
PTP  XHOME
;ENDFOLD
   GOTO SACO
ENDIF
;Endfold

WAIT SEC 0.2
VACIO=FALSE  ;Abro vacio
WAIT FOR NOT VACOESTATO OR VOLVER_COGER ;Espero Vacoestato
WAIT SEC 0.4

;Fold Velocidad Rapida
Continue
FOR ax_num=1 TO 6
$VEL_AXIS[ax_num]=vptpR
$ACC_AXIS[ax_num]=vaccR
ENDFOR
$VEL.CP=vlinR
;Endfold

LIN POS_A C_DIS

;FOLD PTP HOME CONT Vel=ptpM % DEFAULT;%{PE}%R 8.2.24,%MKUKATPBASIS,%CMOVE,%VPTP,%P 1:PTP, 2:HOME, 3:C_DIS, 5:50, 7:DEFAULT
$BWDSTART=FALSE
PDAT_ACT=PDEFAULT
FDAT_ACT=FHOME
BAS(#PTP_PARAMS,vptpM)
$H_POS=XHOME
$APO.CPTP = 70
PTP XHOME C_PTP
;ENDFOLD

;Fold Incremento Saco
N_SACO=N_SACO+1  
IF (N_SACO >= SACOS_A_PALETIZAR) THEN
   GOTO FINAL
ENDIF
;Endfold

ENDFOR

;Fold Incremento Planta
PLANTA = PLANTA + 1

INC_Z=INC_Z+210.0 ;Incremento cota superior


;Endfold

ENDFOR

GOTO FINAL
;Fold PUNTOS COGIDA SOLO PARA MODIFICAR TOUCHAP
;FOLD PTP P1 CONT Vel=50 % PDAT30 Tool[1]:VENTOSA_SACOS Base[2]:PALETS;%{PE}%R 8.2.24,%MKUKATPBASIS,%CMOVE,%VPTP,%P 1:PTP, 2:P1, 3:C_DIS, 5:50, 7:PDAT30
$BWDSTART=FALSE
PDAT_ACT=PPDAT30
FDAT_ACT=FP1
BAS(#PTP_PARAMS,50)
PTP XP1 C_DIS
;ENDFOLD
;FOLD PTP P2 CONT Vel=50 % PDAT31 Tool[1]:VENTOSA_SACOS Base[2]:PALETS;%{PE}%R 8.2.24,%MKUKATPBASIS,%CMOVE,%VPTP,%P 1:PTP, 2:P2, 3:C_DIS, 5:50, 7:PDAT31
$BWDSTART=FALSE
PDAT_ACT=PPDAT31
FDAT_ACT=FP2
BAS(#PTP_PARAMS,50)
PTP XP2 C_DIS
;ENDFOLD
;FOLD PTP P3 CONT Vel=50 % PDAT32 Tool[1]:VENTOSA_SACOS Base[2]:PALETS;%{PE}%R 8.2.24,%MKUKATPBASIS,%CMOVE,%VPTP,%P 1:PTP, 2:P3, 3:C_DIS, 5:50, 7:PDAT32
$BWDSTART=FALSE
PDAT_ACT=PPDAT32
FDAT_ACT=FP3
BAS(#PTP_PARAMS,50)
PTP XP3 C_DIS
;ENDFOLD
;FOLD PTP P4 CONT Vel=50 % PDAT33 Tool[1]:VENTOSA_SACOS Base[2]:PALETS;%{PE}%R 8.2.24,%MKUKATPBASIS,%CMOVE,%VPTP,%P 1:PTP, 2:P4, 3:C_DIS, 5:50, 7:PDAT33
$BWDSTART=FALSE
PDAT_ACT=PPDAT33
FDAT_ACT=FP4
BAS(#PTP_PARAMS,50)
PTP XP4 C_DIS
;ENDFOLD
;FOLD PTP P5 CONT Vel=50 % PDAT34 Tool[1]:VENTOSA_SACOS Base[2]:PALETS;%{PE}%R 8.2.24,%MKUKATPBASIS,%CMOVE,%VPTP,%P 1:PTP, 2:P5, 3:C_DIS, 5:50, 7:PDAT34
$BWDSTART=FALSE
PDAT_ACT=PPDAT34
FDAT_ACT=FP5
BAS(#PTP_PARAMS,50)
PTP XP5 C_DIS
;ENDFOLD
;FOLD PTP P6 CONT Vel=50 % PDAT35 Tool[1]:VENTOSA_SACOS Base[2]:PALETS;%{PE}%R 8.2.24,%MKUKATPBASIS,%CMOVE,%VPTP,%P 1:PTP, 2:P6, 3:C_DIS, 5:50, 7:PDAT35
$BWDSTART=FALSE
PDAT_ACT=PPDAT35
FDAT_ACT=FP6
BAS(#PTP_PARAMS,50)
PTP XP6 C_DIS
;ENDFOLD
;FOLD PTP P7 CONT Vel=50 % PDAT36 Tool[1]:VENTOSA_SACOS Base[2]:PALETS;%{PE}%R 8.2.24,%MKUKATPBASIS,%CMOVE,%VPTP,%P 1:PTP, 2:P7, 3:C_DIS, 5:50, 7:PDAT36
$BWDSTART=FALSE
PDAT_ACT=PPDAT36
FDAT_ACT=FP7
BAS(#PTP_PARAMS,50)
PTP XP7 C_DIS
;ENDFOLD
;FOLD PTP P8 CONT Vel=50 % PDAT37 Tool[1]:VENTOSA_SACOS Base[2]:PALETS;%{PE}%R 8.2.24,%MKUKATPBASIS,%CMOVE,%VPTP,%P 1:PTP, 2:P8, 3:C_DIS, 5:50, 7:PDAT37
$BWDSTART=FALSE
PDAT_ACT=PPDAT37
FDAT_ACT=FP8
BAS(#PTP_PARAMS,50)
PTP XP8 C_DIS
;ENDFOLD
;FOLD PTP P9 CONT Vel=50 % PDAT38 Tool[1]:VENTOSA_SACOS Base[2]:PALETS;%{PE}%R 8.2.24,%MKUKATPBASIS,%CMOVE,%VPTP,%P 1:PTP, 2:P9, 3:C_DIS, 5:50, 7:PDAT38
$BWDSTART=FALSE
PDAT_ACT=PPDAT38
FDAT_ACT=FP9
BAS(#PTP_PARAMS,50)
PTP XP9 C_DIS
;ENDFOLD
;FOLD PTP P10 CONT Vel=50 % PDAT39 Tool[1]:VENTOSA_SACOS Base[2]:PALETS;%{PE}%R 8.2.24,%MKUKATPBASIS,%CMOVE,%VPTP,%P 1:PTP, 2:P10, 3:C_DIS, 5:50, 7:PDAT39
$BWDSTART=FALSE
PDAT_ACT=PPDAT39
FDAT_ACT=FP10
BAS(#PTP_PARAMS,50)
PTP XP10 C_DIS
;ENDFOLD
;Endfold

FINAL:

PULSE ( PALET_COMPLETO, TRUE, 0.5 )
WAIT SEC 1

SALIRPROG:

N_SACO=0
SALIR=FALSE
BOMBA=FALSE

;FOLD PTP HOME CONT Vel=ptpM % DEFAULT;%{PE}%R 8.2.24,%MKUKATPBASIS,%CMOVE,%VPTP,%P 1:PTP, 2:HOME, 3:C_DIS, 5:50, 7:DEFAULT
$BWDSTART=FALSE
PDAT_ACT=PDEFAULT
FDAT_ACT=FHOME
BAS(#PTP_PARAMS,vptpM)
$H_POS=XHOME
$APO.CPTP = 70
PTP XHOME C_PTP
;ENDFOLD

END