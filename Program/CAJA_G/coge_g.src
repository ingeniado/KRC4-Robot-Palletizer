&ACCESS RVO1
&REL 22
&PARAM TEMPLATE = C:\KRC\Roboter\Template\vorgabe
&PARAM EDITMASK = *
DEF COGE_G( )
;Fold Definicion de Variables
DECL E6POS POS_AUX1
DECL REAL INC_Z1
;Endfold
;FOLD INI;%{PE}
  ;FOLD BASISTECH INI
    GLOBAL INTERRUPT DECL 3 WHEN $STOPMESS==TRUE DO IR_STOPM ( )
    INTERRUPT ON 3 
    BAS (#INITMOV,0 )
  ;ENDFOLD (BASISTECH INI)
;FOLD SPOTTECH INI
USERSPOT(#INIT)
;ENDFOLD (SPOTTECH INI)
;FOLD GRIPPERTECH INI
USER_GRP(0,DUMMY,DUMMY,GDEFAULT)
;ENDFOLD (GRIPPERTECH INI)
  ;FOLD USER INI
    ;Make your modifications here

  ;ENDFOLD (USER INI)
;ENDFOLD (INI)
; === PROGRAMA COGIDA CAJA GRANDE ==
;Fold VARIABLES
INC_Z1=400 ;Cota Inicial Seguridad 350
POS_AUX1 =XC1
POS_AUX1.Z=POS_AUX1.Z+INC_Z1
;Endfold

;FOLD PTP HOME1 CONT Vel=100 % DEFAULT;%{PE}%R 8.2.24,%MKUKATPBASIS,%CMOVE,%VPTP,%P 1:PTP, 2:HOME1, 3:C_DIS, 5:50, 7:DEFAULT
$BWDSTART=FALSE
PDAT_ACT=PDEFAULT
FDAT_ACT=FHOME
BAS(#PTP_PARAMS,100)
$H_POS=XHOME1
PTP XHOME1 C_DIS
;ENDFOLD

BAS(#BASE,3)

;Fold Cambio velocidad Rapida
Continue
FOR ax_num=1 TO 6
$VEL_AXIS[ax_num]=vptpR
$ACC_AXIS[ax_num]=vaccR
ENDFOR
$VEL.CP=vlinR
;Endfold

PTP POS_AUX1 C_PTP

;Fold Espero señal PLC: Caja RDY
WAIT FOR CAJA_RDY OR SALIR_PROGRAMA_ACTUAL
IF SALIR_PROGRAMA_ACTUAL == TRUE THEN
   PULSE ( OK_LEIDO_SALIR_PROGRAMA, true, 0.5 )
   SALIR = TRUE
   WAIT SEC 1
   GOTO FINAL
ENDIF
;Endfold

VACIO=TRUE ;Cierro vacio
LIN XC1 ;Punto Cogida Saco

;Fold Espero señal PLC: Vacoestato
WAIT FOR VACOESTATO OR SALIR_PROGRAMA_ACTUAL OR VOLVER_COGER;Espero Vacoestato
IF SALIR_PROGRAMA_ACTUAL == TRUE THEN
   PULSE ( OK_LEIDO_SALIR_PROGRAMA, true, 0.5 )
   SALIR = TRUE
   WAIT SEC 1
   GOTO FINAL
ENDIF
;Endfold

;Fold Cambio velocidad CAJA GRANDE
$VEL.CP=vlinCAJAG
;Endfold

LIN POS_AUX1 C_DIS

CONTINUE
PULSE ( CAJA_COGIDA, TRUE, 0.5 ) ;Indico saco cogido

;FOLD PTP HOME1 CONT Vel=vptpCM % DEFAULT;%{PE}%R 8.2.24,%MKUKATPBASIS,%CMOVE,%VPTP,%P 1:PTP, 2:HOME1, 3:C_DIS, 5:50, 7:DEFAULT
$BWDSTART=FALSE
PDAT_ACT=PDEFAULT
FDAT_ACT=FHOME
BAS(#PTP_PARAMS,vptpCAJAG)
$H_POS=XHOME1
$APO.CPTP = 70
PTP XHOME1 C_PTP
;ENDFOLD

GOTO FINAL

;Fold PUNTOS COGIDA SOLO PARA MODIFICAR TOUCHAP
;Endfold

;FOLD PTP C1 CONT Vel=50 % PDAT1 Tool[1]:VENTOSA_SACOS Base[3]:CAJAS;%{PE}%R 8.2.24,%MKUKATPBASIS,%CMOVE,%VPTP,%P 1:PTP, 2:C1, 3:C_DIS, 5:50, 7:PDAT1
$BWDSTART=FALSE
PDAT_ACT=PPDAT1
FDAT_ACT=FC1
BAS(#PTP_PARAMS,50)
PTP XC1 C_DIS
;ENDFOLD

FINAL:
END
