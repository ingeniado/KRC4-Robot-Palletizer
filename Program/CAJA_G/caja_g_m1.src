&ACCESS RVP
&PARAM DISKPATH = KRC:\R1\Program\CAJA_G
&PARAM TEMPLATE = C:\KRC\Roboter\Template\vorgabe
&PARAM EDITMASK = *
DEF CAJA_G_M1( )
;Fold DECLARACION VARIABLES
DECL E6POS POS_A
DECL E6POS POS_B
DECL E6POS POS_C
DECL E6POS POS_D
DECL INT PLANTA
DECL INT SACOS_A_PALETIZAR
DECL E6POS PUNTO[10]
DECL INT n
DECL REAL INC_Z
EXT COGE_G() 
;Endfold
;FOLD INI;%{PE}
  ;FOLD BASISTECH INI
    GLOBAL INTERRUPT DECL 3 WHEN $STOPMESS==TRUE DO IR_STOPM ( )
    INTERRUPT ON 3 
    BAS (#INITMOV,0 )
  ;ENDFOLD (BASISTECH INI)
;FOLD SPOTTECH INI
USERSPOT(#INIT)
;ENDFOLD (SPOTTECH INI)
;FOLD GRIPPERTECH INI
USER_GRP(0,DUMMY,DUMMY,GDEFAULT)
;ENDFOLD (GRIPPERTECH INI)
  ;FOLD USER INI
    ;Make your modifications here

  ;ENDFOLD (USER INI)
;ENDFOLD (INI)
; === PROGRAMA PALETIZADO CAJAS GRANDES MOSAICO1: 4-2-4 ==
;Fold VARIABLES
PLANTA = 1
N_SACO=0
SACOS_A_PALETIZAR=SACOS_REALZIAR
n=1
INC_Z=5   ;Cota Inicial Dejada
PUNTO[1]=XC1
PUNTO[2]=XC2
PUNTO[3]=XC3
PUNTO[4]=XC4
PUNTO[5]=XC5
PUNTO[6]=XC6
PUNTO[7]=XC7
PUNTO[8]=XC8
PUNTO[9]=XC9
PUNTO[10]=XC10
;Endfold
;FOLD PTP HOME1  Vel= 100 % DEFAULT;%{PE}%MKUKATPBASIS,%CMOVE,%VPTP,%P 1:PTP, 2:HOME1, 3:, 5:100, 7:DEFAULT
$BWDSTART = FALSE
PDAT_ACT=PDEFAULT
FDAT_ACT=FHOME
BAS (#PTP_PARAMS,100 )
$H_POS=XHOME1
PTP  XHOME1
;ENDFOLD
;Fold Compruebo Palet
WAIT FOR PALET_LIBRE OR SALIR_PROGRAMA_ACTUAL
IF SALIR_PROGRAMA_ACTUAL == TRUE THEN
   PULSE ( OK_LEIDO_SALIR_PROGRAMA, true, 0.5 )
   WAIT SEC 1
   GOTO SALIRPROG
ENDIF
;Endfold

BOMBA =TRUE
;=== BUCLE PRINCIPAL ==
FOR PLANTA=1 TO 5 ;Bucle de plantas
FOR n=1 TO 10 ;Bucle de cajas
CAJA:

;Fold Espero seÃ±al PLC:  REPITE_CAJA
IF (ERROR_SACO_CAIDO == TRUE ) THEN
   WAIT FOR REPITE_SACO OR SALIR_PROGRAMA_ACTUAL
   ERROR_SACO_CAIDO = FALSE
   
   IF SALIR_PROGRAMA_ACTUAL == TRUE THEN
   PULSE ( OK_LEIDO_SALIR_PROGRAMA, true, 0.5 )
   WAIT SEC 1
   GOTO SALIRPROG
   ENDIF

ENDIF
;Endfold
COGE_G () ;Llamo programa coger
;Fold CONDICION DE SALIDA DE PROGRAMA PLC
IF SALIR == TRUE THEN
   GOTO SALIRPROG
ENDIF
;Endfold

BAS(#BASE,2) ;Cambio de base
$APO.CPTP = 70
;Fold DEFINICION PUNTOS AUXILIARES
POS_A =PUNTO[n]
POS_B =PUNTO[n]
POS_C =PUNTO[n]
POS_D =PUNTO[n]

POS_A.Y=POS_A.Y+50
POS_A.X=POS_A.X-50
POS_A.Z=POS_A.Z+INC_Z+400

POS_B.Z=POS_B.Z+INC_Z+50

POS_C.Z=POS_C.Z+INC_Z

POS_D.Z=POS_D.Z+INC_Z+400


;Endfold
;Fold Velocidad Media
FOR ax_num=1 TO 6
$VEL_AXIS[ax_num]=vptpCM
$ACC_AXIS[ax_num]=vaccM
ENDFOR
$VEL.CP=vlinCM
;Endfold
PTP POS_A C_PTP
;Fold Velocidad Lenta
FOR ax_num=1 TO 6
$VEL_AXIS[ax_num]=vptpL
$ACC_AXIS[ax_num]=vaccL
ENDFOR
$VEL.CP=vlinL
;Endfold
LIN POS_B C_DIS
LIN POS_C
;Fold Compruebo caida de CAJA
IF (VACOESTATO == FALSE) THEN ;Condicion de saco perdido
   ERROR_SACO_CAIDO = TRUE
   FOR ax_num=1 TO 6
   $VEL_AXIS[ax_num]=vptpR
   $ACC_AXIS[ax_num]=vaccR
   ENDFOR
   $VEL.CP=vlinR
   LIN POS_A
   ;FOLD PTP HOME  Vel= 100 % DEFAULT;%{PE}%MKUKATPBASIS,%CMOVE,%VPTP,%P 1:PTP, 2:HOME, 3:, 5:100, 7:DEFAULT
$BWDSTART = FALSE
PDAT_ACT=PDEFAULT
FDAT_ACT=FHOME
BAS (#PTP_PARAMS,100 )
$H_POS=XHOME
PTP  XHOME
;ENDFOLD
   GOTO CAJA
ENDIF
;Endfold
;Fold Abro vacio y compruebo
WAIT SEC 0.2
VACIO=FALSE  ;Abro vacio
WAIT FOR NOT VACOESTATO ;Espero Vacoestato
;Endfold
;Fold Velocidad Rapida
Continue
FOR ax_num=1 TO 6
$VEL_AXIS[ax_num]=vptpR
$ACC_AXIS[ax_num]=vaccR
ENDFOR
$VEL.CP=vlinR
;Endfold
PTP POS_D C_PTP
;Fold Incremento CAJA
N_SACO=N_SACO+1  
IF (N_SACO >= SACOS_A_PALETIZAR) THEN
   GOTO FINAL
ENDIF
;Endfold
ENDFOR
;Fold Incremento Planta
PLANTA = PLANTA + 1
INC_Z=INC_Z+250.0  ;Incremento cota superior
;Endfold
ENDFOR

GOTO FINAL
;Fold PUNTOS CAJAS
;FOLD PTP C1 Vel=50 % PDAT1 Tool[1]:VENTOSA_SACOS Base[2]:PALETS;%{PE}%R 8.2.24,%MKUKATPBASIS,%CMOVE,%VPTP,%P 1:PTP, 2:C1, 3:, 5:50, 7:PDAT1
$BWDSTART=FALSE
PDAT_ACT=PPDAT1
FDAT_ACT=FC1
BAS(#PTP_PARAMS,50)
PTP XC1 
;ENDFOLD
;FOLD PTP C2 Vel=50 % PDAT2 Tool[1]:VENTOSA_SACOS Base[2]:PALETS;%{PE}%R 8.2.24,%MKUKATPBASIS,%CMOVE,%VPTP,%P 1:PTP, 2:C2, 3:, 5:50, 7:PDAT2
$BWDSTART=FALSE
PDAT_ACT=PPDAT2
FDAT_ACT=FC2
BAS(#PTP_PARAMS,50)
PTP XC2 
;ENDFOLD
;FOLD PTP C3 Vel=50 % PDAT3 Tool[1]:VENTOSA_SACOS Base[2]:PALETS;%{PE}%R 8.2.24,%MKUKATPBASIS,%CMOVE,%VPTP,%P 1:PTP, 2:C3, 3:, 5:50, 7:PDAT3
$BWDSTART=FALSE
PDAT_ACT=PPDAT3
FDAT_ACT=FC3
BAS(#PTP_PARAMS,50)
PTP XC3 
;ENDFOLD
;FOLD PTP C4 Vel=50 % PDAT4 Tool[1]:VENTOSA_SACOS Base[2]:PALETS;%{PE}%R 8.2.24,%MKUKATPBASIS,%CMOVE,%VPTP,%P 1:PTP, 2:C4, 3:, 5:50, 7:PDAT4
$BWDSTART=FALSE
PDAT_ACT=PPDAT4
FDAT_ACT=FC4
BAS(#PTP_PARAMS,50)
PTP XC4 
;ENDFOLD
;FOLD PTP C5 Vel=50 % PDAT5 Tool[1]:VENTOSA_SACOS Base[2]:PALETS;%{PE}%R 8.2.24,%MKUKATPBASIS,%CMOVE,%VPTP,%P 1:PTP, 2:C5, 3:, 5:50, 7:PDAT5
$BWDSTART=FALSE
PDAT_ACT=PPDAT5
FDAT_ACT=FC5
BAS(#PTP_PARAMS,50)
PTP XC5 
;ENDFOLD
;FOLD PTP C6 Vel=50 % PDAT6 Tool[1]:VENTOSA_SACOS Base[2]:PALETS;%{PE}%R 8.2.24,%MKUKATPBASIS,%CMOVE,%VPTP,%P 1:PTP, 2:C6, 3:, 5:50, 7:PDAT6
$BWDSTART=FALSE
PDAT_ACT=PPDAT6
FDAT_ACT=FC6
BAS(#PTP_PARAMS,50)
PTP XC6 
;ENDFOLD
;FOLD PTP C7 Vel=50 % PDAT7 Tool[1]:VENTOSA_SACOS Base[2]:PALETS;%{PE}%R 8.2.24,%MKUKATPBASIS,%CMOVE,%VPTP,%P 1:PTP, 2:C7, 3:, 5:50, 7:PDAT7
$BWDSTART=FALSE
PDAT_ACT=PPDAT7
FDAT_ACT=FC7
BAS(#PTP_PARAMS,50)
PTP XC7 
;ENDFOLD
;FOLD PTP C8 Vel=50 % PDAT8 Tool[1]:VENTOSA_SACOS Base[2]:PALETS;%{PE}%R 8.2.24,%MKUKATPBASIS,%CMOVE,%VPTP,%P 1:PTP, 2:C8, 3:, 5:50, 7:PDAT8
$BWDSTART=FALSE
PDAT_ACT=PPDAT8
FDAT_ACT=FC8
BAS(#PTP_PARAMS,50)
PTP XC8 
;ENDFOLD
;FOLD PTP C9 Vel=50 % PDAT9 Tool[1]:VENTOSA_SACOS Base[2]:PALETS;%{PE}%R 8.2.24,%MKUKATPBASIS,%CMOVE,%VPTP,%P 1:PTP, 2:C9, 3:, 5:50, 7:PDAT9
$BWDSTART=FALSE
PDAT_ACT=PPDAT9
FDAT_ACT=FC9
BAS(#PTP_PARAMS,50)
PTP XC9 
;ENDFOLD
;FOLD PTP C10 Vel=50 % PDAT10 Tool[1]:VENTOSA_SACOS Base[2]:PALETS;%{PE}%R 8.2.24,%MKUKATPBASIS,%CMOVE,%VPTP,%P 1:PTP, 2:C10, 3:, 5:50, 7:PDAT10
$BWDSTART=FALSE
PDAT_ACT=PPDAT10
FDAT_ACT=FC10
BAS(#PTP_PARAMS,50)
PTP XC10 
;ENDFOLD
;Endfold

FINAL:

PULSE ( PALET_COMPLETO, TRUE, 0.5 )
WAIT SEC 1

SALIRPROG:

N_SACO=0
SALIR=FALSE
BOMBA=FALSE

;FOLD PTP HOME CONT Vel=ptpM % DEFAULT;%{PE}%R 8.2.24,%MKUKATPBASIS,%CMOVE,%VPTP,%P 1:PTP, 2:HOME, 3:C_DIS, 5:50, 7:DEFAULT
$BWDSTART=FALSE
PDAT_ACT=PDEFAULT
FDAT_ACT=FHOME
BAS(#PTP_PARAMS,vptpM)
$H_POS=XHOME
$APO.CPTP = 70
PTP XHOME C_PTP
;ENDFOLD

;FOLD PTP HOME  Vel= 50 % DEFAULT;%{PE}%MKUKATPBASIS,%CMOVE,%VPTP,%P 1:PTP, 2:HOME, 3:, 5:100, 7:DEFAULT
$BWDSTART = FALSE
PDAT_ACT=PDEFAULT
FDAT_ACT=FHOME
BAS (#PTP_PARAMS,50 )
$H_POS=XHOME
PTP  XHOME
;ENDFOLD

END