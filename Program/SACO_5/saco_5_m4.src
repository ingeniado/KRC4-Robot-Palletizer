&ACCESS RVP1
&REL 9
&PARAM DISKPATH = KRC:\R1\Program\SACO_5
&PARAM TEMPLATE = C:\KRC\Roboter\Template\vorgabe
&PARAM EDITMASK = *
DEF saco_5_m4( )
;Fold DECLARACION VARIABLES
DECL E6POS POS_A
DECL E6POS POS_B
DECL E6POS POS_C
DECL REAL INC_Z
DECL INT PLANTA
DECL INT SACOS_A_PALETIZAR
DECL E6POS PUNTO[20]
DECL INT n
EXT COGE_5 () 
;Endfold
;FOLD INI;%{PE}
  ;FOLD BASISTECH INI
    GLOBAL INTERRUPT DECL 3 WHEN $STOPMESS==TRUE DO IR_STOPM ( )
    INTERRUPT ON 3 
    BAS (#INITMOV,0 )
  ;ENDFOLD (BASISTECH INI)
;FOLD SPOTTECH INI
USERSPOT(#INIT)
;ENDFOLD (SPOTTECH INI)
;FOLD GRIPPERTECH INI
USER_GRP(0,DUMMY,DUMMY,GDEFAULT)
;ENDFOLD (GRIPPERTECH INI)
  ;FOLD USER INI
    ;Make your modifications here

  ;ENDFOLD (USER INI)
;ENDFOLD (INI)
; === PROGRAMA 200 SACOS 5 KG MOSAICO 10-10 ==
;Fold VARIABLES
INC_Z=0  ;Cota Inicial
PLANTA = 0
N_SACO=0
SACOS_A_PALETIZAR=200
n=1
PUNTO[1]=XP1
PUNTO[2]=XP2
PUNTO[3]=XP3
PUNTO[4]=XP4
PUNTO[5]=XP5
PUNTO[6]=XP6
PUNTO[7]=XP7
PUNTO[8]=XP8
PUNTO[9]=XP9
PUNTO[10]=XP10
PUNTO[11]=XP11
PUNTO[12]=XP12
PUNTO[13]=XP13
PUNTO[14]=XP14
PUNTO[15]=XP15
PUNTO[16]=XP16
PUNTO[17]=XP17
PUNTO[18]=XP18
PUNTO[19]=XP19
PUNTO[20]=XP20
;Endfold
;FOLD PTP HOME  Vel= 100 % DEFAULT;%{PE}%MKUKATPBASIS,%CMOVE,%VPTP,%P 1:PTP, 2:HOME, 3:, 5:100, 7:DEFAULT
$BWDSTART = FALSE
PDAT_ACT=PDEFAULT
FDAT_ACT=FHOME
BAS (#PTP_PARAMS,100 )
$H_POS=XHOME
PTP  XHOME
;ENDFOLD
;Fold Compruebo Palet
WAIT FOR PALET_LIBRE OR SALIR_PROGRAMA_ACTUAL
IF SALIR_PROGRAMA_ACTUAL == TRUE THEN
   PULSE ( OK_LEIDO_SALIR_PROGRAMA, true, 0.5 )
   WAIT SEC 1
   GOTO SALIRPROG
ENDIF
;Endfold

BOMBA =TRUE
;=== BUCLE PRINCIPAL ==
FOR PLANTA=0 TO 30 ;Bucle de plantas
   
FOR n=1 TO 20 ;Bucle de sacos
SACO:

;Fold WAIT FOR REPITE_SACO: Espero repetir saco si ha caido
IF (ERROR_SACO_CAIDO == TRUE ) THEN
   WAIT FOR REPITE_SACO OR SALIR_PROGRAMA_ACTUAL
   ERROR_SACO_CAIDO = FALSE
ENDIF
;Endfold
COGE_5 () ;Llamo programa coger
;Fold CONDICION DE SALIDA DE PROGRAMA PLC
IF SALIR == TRUE THEN
   GOTO SALIRPROG
ENDIF
;Endfold

;Fold DEFINICION PUNTOS AUXILIARES
POS_A =PUNTO[n]
POS_B =PUNTO[n]
POS_C =PUNTO[n]

POS_A.Z=POS_A.Z+INC_Z+400
POS_B.Z=POS_B.Z+INC_Z+250
POS_C.Z=POS_C.Z+INC_Z+60

;Endfold

BAS(#BASE,2) ;Cambio de base
$APO.CPTP = 70


;Fold Velocidad SACO5
FOR ax_num=1 TO 6
$VEL_AXIS[ax_num]=vptpSACO5
$ACC_AXIS[ax_num]=vaccSACO5
ENDFOR
$VEL.CP=vlinSACO5
;Endfold
PTP POS_A C_PTP
;Fold Velocidad Media
FOR ax_num=1 TO 6
$VEL_AXIS[ax_num]=vptpM
$ACC_AXIS[ax_num]=vaccM
ENDFOR
$VEL.CP=vlinM
;Endfold
PTP POS_B C_PTP
;Fold Velocidad Lenta
FOR ax_num=1 TO 6
$VEL_AXIS[ax_num]=vptpL
$ACC_AXIS[ax_num]=vaccL
ENDFOR
$VEL.CP=vlinL
;Endfold
LIN POS_C 
;Fold Compruebo caida de saco
IF (VACOESTATO == FALSE) THEN ;Condicion de saco perdido
   ERROR_SACO_CAIDO = TRUE
   FOR ax_num=1 TO 6
   $VEL_AXIS[ax_num]=vptpR
   $ACC_AXIS[ax_num]=vaccR
   ENDFOR
   $VEL.CP=vlinR
   LIN POS_A
   ;FOLD PTP HOME  Vel= 100 % DEFAULT;%{PE}%MKUKATPBASIS,%CMOVE,%VPTP,%P 1:PTP, 2:HOME, 3:, 5:100, 7:DEFAULT
$BWDSTART = FALSE
PDAT_ACT=PDEFAULT
FDAT_ACT=FHOME
BAS (#PTP_PARAMS,100 )
$H_POS=XHOME
PTP  XHOME
;ENDFOLD
   GOTO SACO
ENDIF
;Endfold

WAIT SEC 0.2
VACIO=FALSE  ;Abro vacio
WAIT FOR NOT VACOESTATO OR VOLVER_COGER ;Espero Vacoestato

;Fold Velocidad Rapida
Continue
FOR ax_num=1 TO 6
$VEL_AXIS[ax_num]=vptpR
$ACC_AXIS[ax_num]=vaccR
ENDFOR
$VEL.CP=vlinR
;Endfold

LIN POS_A C_DIS

;Fold Incremento Saco
N_SACO=N_SACO+1  
IF (N_SACO >= SACOS_A_PALETIZAR) THEN
   GOTO FINAL
ENDIF
;Endfold

ENDFOR

;Fold Incremento Planta
PLANTA = PLANTA + 1

INC_Z=INC_Z+95.0  ;Incremento cota superior

;Endfold

ENDFOR

GOTO FINAL
;Fold PUNTOS COGIDA SOLO PARA MODIFICAR TOUCHAP

;FOLD PTP S1 Vel=50 % PDAT1 Tool[1]:VENTOSA_SACOS Base[2]:PALETS;%{PE}%R 8.2.24,%MKUKATPBASIS,%CMOVE,%VPTP,%P 1:PTP, 2:S1, 3:, 5:50, 7:PDAT1
$BWDSTART=FALSE
PDAT_ACT=PPDAT1
FDAT_ACT=FS1
BAS(#PTP_PARAMS,50)
PTP XS1 
;ENDFOLD
;FOLD PTP S2 Vel=50 % PDAT2 Tool[1]:VENTOSA_SACOS Base[2]:PALETS;%{PE}%R 8.2.24,%MKUKATPBASIS,%CMOVE,%VPTP,%P 1:PTP, 2:S2, 3:, 5:50, 7:PDAT2
$BWDSTART=FALSE
PDAT_ACT=PPDAT2
FDAT_ACT=FS2
BAS(#PTP_PARAMS,50)
PTP XS2 
;ENDFOLD
;FOLD PTP S3 Vel=50 % PDAT3 Tool[1]:VENTOSA_SACOS Base[2]:PALETS;%{PE}%R 8.2.24,%MKUKATPBASIS,%CMOVE,%VPTP,%P 1:PTP, 2:S3, 3:, 5:50, 7:PDAT3
$BWDSTART=FALSE
PDAT_ACT=PPDAT3
FDAT_ACT=FS3
BAS(#PTP_PARAMS,50)
PTP XS3 
;ENDFOLD
;FOLD PTP S4 Vel=50 % PDAT4 Tool[1]:VENTOSA_SACOS Base[2]:PALETS;%{PE}%R 8.2.24,%MKUKATPBASIS,%CMOVE,%VPTP,%P 1:PTP, 2:S4, 3:, 5:50, 7:PDAT4
$BWDSTART=FALSE
PDAT_ACT=PPDAT4
FDAT_ACT=FS4
BAS(#PTP_PARAMS,50)
PTP XS4 
;ENDFOLD
;planta2
;FOLD PTP S5 Vel=50 % PDAT5 Tool[1]:VENTOSA_SACOS Base[2]:PALETS;%{PE}%R 8.2.24,%MKUKATPBASIS,%CMOVE,%VPTP,%P 1:PTP, 2:S5, 3:, 5:50, 7:PDAT5
$BWDSTART=FALSE
PDAT_ACT=PPDAT5
FDAT_ACT=FS5
BAS(#PTP_PARAMS,50)
PTP XS5 
;ENDFOLD
;FOLD PTP S6 Vel=50 % PDAT6 Tool[1]:VENTOSA_SACOS Base[2]:PALETS;%{PE}%R 8.2.24,%MKUKATPBASIS,%CMOVE,%VPTP,%P 1:PTP, 2:S6, 3:, 5:50, 7:PDAT6
$BWDSTART=FALSE
PDAT_ACT=PPDAT6
FDAT_ACT=FS6
BAS(#PTP_PARAMS,50)
PTP XS6 
;ENDFOLD
;FOLD PTP S7 Vel=50 % PDAT7 Tool[1]:VENTOSA_SACOS Base[2]:PALETS;%{PE}%R 8.2.24,%MKUKATPBASIS,%CMOVE,%VPTP,%P 1:PTP, 2:S7, 3:, 5:50, 7:PDAT7
$BWDSTART=FALSE
PDAT_ACT=PPDAT7
FDAT_ACT=FS7
BAS(#PTP_PARAMS,50)
PTP XS7 
;ENDFOLD
;FOLD PTP S8 Vel=50 % PDAT8 Tool[1]:VENTOSA_SACOS Base[2]:PALETS;%{PE}%R 8.2.24,%MKUKATPBASIS,%CMOVE,%VPTP,%P 1:PTP, 2:S8, 3:, 5:50, 7:PDAT8
$BWDSTART=FALSE
PDAT_ACT=PPDAT8
FDAT_ACT=FS8
BAS(#PTP_PARAMS,50)
PTP XS8 
;ENDFOLD
;Endfold
FINAL:

PULSE ( PALET_COMPLETO, TRUE, 0.5 )
WAIT SEC 1

SALIRPROG:

N_SACO=0
SALIR=FALSE
BOMBA=FALSE

;FOLD PTP HOME CONT Vel=ptpM % DEFAULT;%{PE}%R 8.2.24,%MKUKATPBASIS,%CMOVE,%VPTP,%P 1:PTP, 2:HOME, 3:C_DIS, 5:50, 7:DEFAULT
$BWDSTART=FALSE
PDAT_ACT=PDEFAULT
FDAT_ACT=FHOME
BAS(#PTP_PARAMS,vptpM)
$H_POS=XHOME
$APO.CPTP = 70
PTP XHOME C_PTP
;ENDFOLD

END